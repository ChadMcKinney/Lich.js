--Mountains of Madness

-- walk north
--Runes of warding
--Traps / Curses
--One player, Multiple Monsters
--See everyone's text fields, but translate enemy text fields into "lovecraftian" language
--Fixed map, with randomized contents

-- Low: 10 kb/s Med: 50 kb/s High: 200 kb/s

--Story KEYS instead of STORY in demoGame object? Optimization for laterrrr

--pooling data structure.

------------------------------------------------------------------------------------------------
--Data Types
------------------------------------------------------------------------------------------------

--

importjs "Pieces/MountainsOfMadnessPanel.js"

data Item {
	name  			= "Not an item.",
	farDescription  = "Not an item",
	description 	= "Not an Item.",
	carryable		= True,
	action 			= Nothing,
	curse 			= Nothing
}

data Trap {
	name 			= "Acid Trap",
	description 	= "A jet of acid suddenly shoots from the ground under you. You are burned.",
	attack 			= 20,
	dementia		= 5
}

data Location {
	name 			= "Nowhere",
	north 			= "Nowhere",
	south 			= "Nowhere",
	east 			= "Nowhere",
	west 			= "Nowhere",
	up 				= "Nowhere",
	down 			= "Nowhere",
	description 	= "Nothing to see here.",
	farDescription 	= "Nothing to see in the distance.",
	items 			= (),
	moveable 		= False
}

data Player {
	name 		= "New Player",
	health 		= 100,
	location 	= "Nowhere",
	description = "A rather academic looking researcher. Cold, but healthy.",
	items 		= (),
	sanity 		= 100
}

data Monster {
	name 		= "Nobody",
	health 		= 100,
	location 	= "Nowhere",
	description = "Not actually a monster.",
	items 		= (),
	attack 		= 25,
	dementia	= 25
}

data Game {
	players		= ("Nobody" = Player),
	monsters 	= ("Nobody" = Monster),
	locations 	= ("Nowhere" = Location),
	narration 	= "",
	gameOver	= False
}

data Cardinal = North | South | East | West | Up | Down

data GameMessage = Walk | Use | Look | Start | Finish | Intro | View | Pickup | Putdown | Inventory | Talk | Status

north 	= North
south 	= South
east 	= East
west 	= West
up 		= Up
down 	= Down

------------------------------------------------------------------------------------------------
--Game Actions
------------------------------------------------------------------------------------------------

-- |addNarration String -> Game -> Game
addNarration story game = game{narration = (game::narration)+" "+story}

-- |addNarration Item -> Game -> Game
addFarDescriptionToNarration item game = game{narration = (game::narration)+" "+(item::farDescription)}

-- |addLocationNarration Location -> Game -> Game
addLocationNarration location game = itemNarratedGame
	where
		locationNarratedGame 	= addNarration (location::description) game
		itemNarratedGame 		= foldr (addFarDescriptionToNarration) locationNarratedGame (location::items)

addPlayerLocationNarration playerName game = addLocationNarration (getLocation ((getSomething playerName game)::location) game) game

narrateStatus player game = 
	case player of
		Player 	-> sanityGame $ healthGame game
		Monster -> monsterGame game
	where
		healthGame game
			| (player::health) <25 	= narrateGame "I'm severely wounded and losing blood," game
			| (player::health) <50 	= narrateGame "I'm moderately wounded," game
			| (player::health) <75 	= narrateGame "I'm lightly wounded," game
			| (player::health) <100 = narrateGame "I'm a bit scuffed up, but it's nothing too serious," game
			| otherwise				= narrateGame "I'm fighting fit," game
		sanityGame game
			| (player::sanity) <25  = narrateGame "and HAHAHA! IA! IA! CTHULHU FHTAGN!" game
			| (player::sanity) <50  = narrateGame "and I'm certain this must.....must be a dream. It must be...." game
			| (player::sanity) <75  = narrateGame "and I'm questioning the things that I'm seeing." game
			| (player::sanity) <100 = narrateGame "and I feel a little unsettled." game
			| otherwise				= narrateGame "and feel right as rain." game
		monsterGame game
			| (player::health) <25 	= narrateGame "I'm severely wounded and losing blood." game
			| (player::health) <50 	= narrateGame "I'm moderately wounded." game
			| (player::health) <75 	= narrateGame "I'm lightly wounded." game
			| (player::health) <100 = narrateGame "I'm a bit scuffed up, but it's nothing too serious." game
			| otherwise				= narrateGame "I'm fighting fit." game	

-- |placeItemAtLocation Item -> Location -> Location
placeItemAtLocation item location = location {items = newItems}
	where
		newItems = ((item :: name) = item) : (location :: items)

-- |removeItemFromLocation Item -> Location -> Location
removeItemFromLocation item location = location{items = newItems}
	where
		newItems = remove (item::name) (location::items)

-- |giveItemToPlayer Item -> Player -> Player
giveItemToPlayer item player = player{items = newItems}
	where
		newItems = ((item::name) = item) : (player::items)

-- |removeItemFromPlayer Item -> Player -> Player
removeItemFromPlayer item player = player{items = newItems}
	where
		newItems = remove (item::name) (player::items)

-- |updateLocation Location -> Game -> Game
updateLocation location game = game{locations = newLocations}
	where
		newLocations = ((location::name) = location) : (game::locations)

-- | addLocations:: Dict -> Game -> Game
addLocations newLocations game = game{locations = combinedLocations}
	where
		combinedLocations = (game::locations) : newLocations

-- |playerLocation Player -> Game -> Location
playerLocation player game = (game::locations) !! (player::location)

-- |getLocation String -> Game -> Player
getLocation locationName game = game::locations !! locationName

-- |getPlayer String -> Game -> Player
getPlayer playerName game = game::players !! playerName

-- |getMonster String -> Game -> Player
getMonster monsterName game = game::monsters !! monsterName

-- |getItem  String -> Player -> Game -> Item
getItem itemName player game = (playerLocation player game)::items !! itemName

getPlayerItem itemName player game = (player)::items !! itemName

--getsomething String -> Game -> (Player | Monster | Location)
getSomething name game = player ? monster ? location
	where
		player 		= getPlayer name game
		monster 	= getMonster name game
		location 	= getLocation name game

-- |updatePlayer Player -> Game -> Game
updatePlayer player game = game{players = newPlayers}
	where
		newPlayers = ((player::name) = player) : (game::players)

-- |updateMonster Monster -> Game -> Game
updateMonster monster game = game{monsters = newMonsters}
	where
		newMonsters = (monster::name = monster) : (game::monsters)

-- |moveItemFromPlayerToLocation Item -> Location -> Player -> Game -> Game
moveItemFromPlayerToLocation item player game = updatePlayer newPlayer newGame
	where
		location 	= getLocation (player::location) game
		newLocation = placeItemAtLocation 	item location
		newPlayer 	= removeItemFromPlayer 	item player
		newGame		= updateLocation newLocation game

-- | pickupCarryableItemFromLocation Item -> Player -> Game -> Game
pickupCarryableItemFromLocation item player game = updatePlayer newPlayer newGame
	where
		location 	= getLocation (player::location) game
		newLocation = removeItemFromLocation item location
		newPlayer 	= giveItemToPlayer 		 item player
		newGame		= updateLocation newLocation game

-- | pickupItemFromLocation String -> Player -> Game -> Game
pickupItemFromLocation itemName player game = newGame $ getItem itemName player game
	where
		newGame item
			| item == Nothing  	= addNarration "I don't see anything to pick up with that name." game
			| item::carryable 	= addNarration ("Ok, I now have a " + itemName) $ pickupCarryableItemFromLocation item player game  
			| otherwise 		= addNarration "I can't carry that!" game

-- | putdownItem String -> Player -> Game -> Game
putdownItem itemName player game = newGame $ getPlayerItem itemName player game
	where
		newGame item
			| item == Nothing 	= addNarration "I don't have anything with that name to put down." game
			| otherwise			= addNarration ("Ok, I put the "+itemName+" down.") $ moveItemFromPlayerToLocation item player game

-- | narrateInventory Player -> Game -> Game
narrateInventory player game = itemNarratedGame
	where
		addNameToNarration item g 	= addNarration ((item::name)+"<br>") g
		newGame						= addNarration "This is what I have: <br>" game
		itemNarratedGame 			= foldr (addNameToNarration) newGame (player::items)

-- | locationAtCardinal Cardinal -> Player -> Game -> Location
locationAtCardinal cardinal player game = newLocation
	where
		oldLocation = playerLocation player game
		newLocation =
			case cardinal of
				North -> getLocation (oldLocation :: north) game
				South -> getLocation (oldLocation :: south) game
				East  -> getLocation (oldLocation :: east ) game
				West  -> getLocation (oldLocation :: west ) game
				Up    -> getLocation (oldLocation :: up   ) game
				Down  -> getLocation (oldLocation :: down ) game

-- |lookAtCardinal :: Cardinal -> Player -> Game -> Game
lookAtCardinal cardinal player game = addNarration description game
	where
		description = (locationAtCardinal cardinal player game) :: farDescription

-- |lookAtItem :: String -> Player -> Game -> Game
lookAtItem itemName player game = newGame (getItem itemName player game) (getPlayerItem itemName player game)
	where
		newGame item1 item2
			| item1 /= Nothing 	= addNarration (item1::description) game
			| item2 /= Nothing 	= addNarration (item2::description) game
			| otherwise			= addNarration "I don't see anything by that name." game

-- |movePlayer :: Cardinal -> Player -> Game -> Game
movePlayer cardinal player game = 
	case newLocation::moveable of
		True	-> newGame
		False	-> addNarration "That way is blocked." game 
	where
		newLocation 	= locationAtCardinal cardinal player game
		newLocationName = newLocation::name
		newPlayer 		= player{location=newLocationName}
		newGame			= addLocationNarration newLocation $ updatePlayer newPlayer game

moveMonster cardinal monster game = 
	case newLocation::moveable of
		True	-> updateMonster newMonster game
		False	-> addNarration "That way is blocked." game 
	where
		newLocation 	= locationAtCardinal cardinal player game
		newMonster 		= monster{location=newLocation::name}

movePlayerOrMonster cardinal name game = newGame
	where
		playerOrMonster = getSomething name game
		newGame =
			case playerOrMonster of
				Monster 	-> moveMonster cardinal playerOrMonster game
				Player 		-> movePlayer cardinal playerOrMonster game
				Location	-> game
	
-- |checkPlayerDeath Player Game -> Game
checkPlayerDeath player game = 
	case player::health < 1 of
		True	-> (addNarration "You died." game){gameOver = True}
		False	-> game

-- |damagePlayer Int Player Game = Game
damagePlayer damage player game = checkPlayerDeath damagedPlayer newGame
	where
		newHealth		= player::health - damage
		damagedPlayer 	= player {health = newHealth}
		newGame 		= updatePlayer damagedPlayer game

-- |monsterAttacksPlayer Monster Player Game = Game
monsterAttacksPlayer monster player game = damagePlayer (monster::attack) player game

updateItemInHolder item itemName holder game = newGame
	where
		newHolder = (itemName=item) : holder::items
		newGame =
			case newHolder of
				Monster 	-> updateMonster newHolder game
				Player	 	-> updatePlayer newHolder game
				Location 	-> updateLocation newHolder game

eraseItemInHolder item holder game = newGame
	where
		newHolder = holder{items = remove (item::name) (holder::items)}
		newGame =
			case newHolder::dataType of
				Monster 	-> updateMonster newHolder game
				Player	 	-> updatePlayer newHolder game
				Location 	-> updateLocation newHolder game
				
curseItem item aCurse location game = updateItemInHolder newItem (item::name) location game
	where
		newItem = item{curse = aCurse}

nullAction item holder receiver game = addNarration game "That has no effect."

--useItemOnSomething String -> String -> String -> Game -> Game
useItemOnSomething playerName itemName receiverName game = (item::action) item player something game
	where
		player 		= getPlayer playerName game
		item 		= play::items !! itemName
		something 	= getSomething

--nullAction:: Item -> a -> b -> Game -> Game
nullAction item holder receiver game = addNarration game "That has no effect."

--closedMetalDoor :: String -> Item
closedMetalDoor name = Item name farDescription description carryable action curse
	where
		farDescription  = "A sturdy looking metal door. It's closed."
		description 	= "Seems to be made of stern stuff. Not getting through here without the key."
		carryable		= False
		action 			= Nothing
		curse 			= Nothing

--openedMetalDoor :: String -> Item
openedMetalDoor name = Item name farDescription description carryable action curse
	where
		farDescription  = "A sturdy looking metal door. It's wide open."
		description 	= "Seems to be made of stern stuff."
		carryable		= False
		action 			= Nothing
		curse 			= Nothing

--openDoorWithKey :: Item -> Player -> Location -> Game -> Game
openDoorWithKey key player door location game = newGame
	where
		newDoor 	= openedMetalDoor "OpenMetalDoor"
		newPlayer 	= removeItemFromPlayer key player
		newLocation = placeItemAtLocation newDoor
		newGame 	= 
			case member (door::name) (location::items) of
				True	-> updatePlayer newPlayer $ updateLocation newLocation game
				False	-> game

--closeDoor :: String -> Location -> Game -> Game
closeDoor doorName location game = newGame
	where
		newDoor 	= closedMetalDoor "ClosedMetalDoor"
		newLocation = placeItemAtLocation newDoor
		newGame 	=
			case member (doorName) (location::items) of
				True	-> updateLocation newLocation game
				False	-> game

--key :: Item
key = Item name farDescription description carryable action curse
	where
		name  			= "Key"
		farDescription  = "A metal key."
		description 	= "The key appears to be made from some kind of metal. Strangely you can feel a slight vibration and warmth emmiting from it."
		carryable		= True
		action 			= openDoorWithKey
		curse 			= Nothing

hilightText text = "<span Class=itemText>" + text + "</span>"


------------------------------------------------------------------------------------------------
--Items and Locations
------------------------------------------------------------------------------------------------

--Zork Above ground
--29 	Rooms
--40ish Connections

--Mountains of Madness
--18 	Rooms
--19 	Connections

--Baroque leit motiff?

------------------------------------------------------------------------------------------------
--ASCII MAP
------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------
----------------------------------------------[Exit2]-------------------------------------------
------------------------------------------------||----------------------------------------------
------------------------------------[Exit1]==[IcePlateau]==[Exit3]------------------------------
-----------------------------------------------||-----------------------------------------------
--------------------------------------------[RuneStairs]----------------------------------------
-----------------------------------------------U|-----------------------------------------------
-----------------------------------------------U|-----------------------------------------------
---------------------------[SecretVault]====[RuneTunnel]----------------------------------------
-------------------------------||---------------------------------------------------------------
------------------------||===[Library]===||-----------------------------------------------------
------------------------||---------------||-----------------------------------------------------
------------------------||---------------||-----------------------------------------------------
----------------[UndergroundLake]===[CityCenter]------------------------------------------------
-----------------------------------------||-----------------------------------------------------
-----------------------------||====[CityEntrance]===||------------------------------------------
-----------------------------||---------------------||------------------------------------------
-------------------------[UnderRuins]-----------[MuralsBottom]----------------------------------
-----------------------------D|---------------------D|------------------------------------------
--------------------------[Ruins]===[Mountains]===[MuralsTop]-----------------------------------
-----------------------------------------||-----------------------------------------------------
-----------------------------------------||-----------------------------------------------------
---------------------------------||====[mound]====||--------------------------------------------
---------------------------------||      ||       ||--------------------------------------------
-----------------------------[mound2]==[Camp]==[mound3]-----------------------------------------
------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------
------------------------------------------------------------------------------------------------

acidTrap		= Trap name desc attack demen
	where
		name 	= "Acid Trap"
		desc 	= "A jet of acid suddenly shoots from the ground under you. You are burned."
		attack 	= 20
		demen	= 5

revelation		= Trap name desc attack demen
	where
		name 	= "A hideous revelation"
		desc 	= "All at once an insane revelation enters your mind. You are overcome with a vision from beyond dimensions, revealing to you just how small a mote your existence is in comparision to the vast NOTHINGNESS of the entire universe. You feel yourself losing grip with sanity."
		attack 	= 0
		demen	= 20

-- |dogCorpse 	::Item
dogCorpse 		= Item name fDesc desc
	where
		name 	= "dog corpse"
		fDesc 	= "On the ground lays a dessicated " + (hilightText "dog corpse")+ ". It seems to have been here awhile."
		desc 	= "Upon closer inspection I find that it appears to have been mauled. The teeth, or claw, wounds seemed to have left a star like pattern."

-- |corpse2 	::Item
corpse 			= Item name fDesc desc
	where
		name 	= "corpse"
		fDesc 	= "The dessicated " + (hilightText "corpse")+ " of an antarctic researcher."
		desc 	= "It appears to have been...dissected. The brain is removed, as are several organs. The incisions appear to have been done with surgical precision."

corpse2 		= Item name fDesc desc
	where
		name 	= "bloated corpse"
		fDesc 	= "The " + (hilightText "bloated corpse")+ " of a scientist"
		desc 	= "Several of the scientist's limbs appear to have been amputated. This was not done by a wild animal as the wounds have been cauterized."

diary			= Item name fDesc desc
	where
		name 	= "diary"
		fDesc 	= "Amongst the tattered bedrolls I see a worn " + (hilightText "diary") + "."
		desc 	= "The diary contains indepth analyses of several in-field dissections conducted."

camp 			= Location "Camp" n s e w u d desc fDesc items mov
	where
		n 		= "Mound"
		s 		= "Nowhere"
		e 		= "Mound3"
		w 		= "Mound2"
		u 		= "Nowhere"
		d 		= "Nowhere"
		--desc 	= "I stood amongst the ruins of my forward scout's base camp. They had gone missing. We are part of a small group of biologists exploring the reaches of Antartica. To the north, east, and west I can see three prominent mounds of snow. Given the scout's absence, this seemed ominous. "
		desc 	= "I stand amongst the ruins of my forward scout's base camp. To the north, east, and west I can see three prominent mounds of snow. Given the scout's absence, this seems ominous. "
		fDesc 	= "You see a white blasted, frigid looking camp."	
		items 	= ("diary" = diary)
		mov 	= True

mound 		= Location "Mound" n s e w u d desc fDesc items mov
	where
		n 		= "Mountains"
		s 		= "Camp"
		e 		= "Mound3"
		w 		= "Mound2"
		u 		= "Nowhere"
		d 		= "Nowhere"
		desc 	= "Standing before me is a strange greenish mound in the snow. To the north are a series of mountains. To the south is the scout camp."
		fDesc 	= "the vauge outline of a small hill of some kind in the snow."	
		items 	= ("dog corpse" = dogCorpse)
		mov 	= True

mound2  		= Location "Mound2" n s e w u d desc fDesc items mov
	where
		n 		= "Mound"
		s 		= "Nowhere"
		e 		= "Camp"
		w 		= "Nowhere"
		u 		= "Nowhere"
		d 		= "Nowhere"
		desc 	= "I am standing above a strange redish mound in the snow. To the east is the scout camp, to the north is another of these mounds."
		fDesc 	= "the vauge outline of a small hill of some kind in the snow."	
		items 	= ("corpse" = corpse)
		mov 	= True

mound3 			= Location "Mound3" n s e w u d desc fDesc items mov
	where
		n 		= "Mound"
		s 		= "Nowhere"
		e 		= "Nowhere"
		w 		= "Camp"
		u 		= "Nowhere"
		d 		= "Nowhere"
		desc 	= "I am standing on a strange redish mound in the snow. To the west is the scout camp, to the north is another of these mounds."
		fDesc 	= "the vauge outline of a small hill of some kind in the snow."	
		items 	= ("bloated corpse" = corpse2)
		mov 	= True

mountains 	 	= Location "Mountains" n s e w u d desc fDesc items mov
	where
		n 		= "Nowhere"
		s 		= "Mound"
		e 		= "Murals"
		w 		= "Ruins"
		u 		= "Nowhere"
		d 		= "Nowhere"
		desc 	= "A series of precipitous and daunting cliffs. Arduous hiking."
		fDesc 	= "the peaks of a chain of peaks and cliffs. Looks dangerous."	
		items 	= ()
		mov 	= True	

ruins 	 		= Location "Ruins" n s e w u d desc fDesc items mov
	where
		n 		= "Nowhere"
		s 		= "Nowhere"
		e 		= "Mountains"
		w 		= "Nowhere"
		u 		= "Nowhere"
		d 		= "UnderRuins"
		desc 	= "There are eldritch ruins; crumbling and falling, yet still holding some of their previous strange, and even dangerous, beauty."
		fDesc 	= "What appears to be ancient ruins of some sort of city."	
		items 	= ()
		mov 	= True

carvings 		= Item name fDesc desc
	where
		name 	= "Carvings"
		fDesc 	= "Ornate carvings."
		desc 	= "Otherworldly carvings which depict, minimally yet beautifully, a large underground city. On closer inspection strange creatures can be seen inhabiting the structures. They appear to be dissecting...or dining...on something."

underRuins 		= Location "UnderRuins" n s e w u d desc fDesc items mov
	where
		n 		= "CityEntrance"
		s 		= "Nowhere"
		e 		= "Nowhere"
		w 		= "Nowhere"
		u 		= "Ruins"
		d 		= "UnderRuins"
		desc 	= "The structures open up into an intricately carved passageway."
		fDesc 	= "There seems to be some kind of passageway leading underground."	
		items 	= ("Carvings" = carvings)
		mov 	= True


mural 	 		= Item name fDesc desc
	where
		name 	= "mural"
		fDesc 	= "Some kind of " + (hilightText "mural")+ "."
		desc 	= "An intricately constructed mural, made of some kind of crystalline material. Seems almost religious or iconic in nature. Depicts a vaugely orb shaped creature with a multitude of appendages and sub-orb like structures. It appears to be swallowing something...or perhaps talking to it?"

murals 	 		= Location "Murals" n s e w u d desc fDesc items mov
	where
		n 		= "Nowhere"
		s 		= "Nowhere"
		e 		= "Nowhere"
		w 		= "Mountains"
		u 		= "Nowhere"
		d 		= "UnderMurals"
		desc 	= "A strange room-like structure, made of an unknown metallic material, and containing several crystalline murals. A portal seems to lead deeper down."
		fDesc 	= "There appears to be ruins of somekind, with glints of glass gleaming from them."	
		items 	= ("mural" = mural)
		mov 	= True

mural2  		= Item name fDesc desc
	where
		name 	= "mural"
		fDesc 	= "Some kind of " + (hilightText "mural")+ "."
		desc 	= ""

underMurals 	= Location "UnderMurals" n s e w u d desc fDesc items mov
	where
		n 		= "CityEntrance"
		s 		= "Nowhere"
		e 		= "Nowhere"
		w 		= "Nowhere"
		u 		= "Murals"
		d 		= "Nowhere"
		desc 	= "The portal leads deeper into the structure. There are more murals, and though you have clearly descended several hundred feet below the surface, an impossible light still radiates from behind them."
		fDesc 	= "It appears to be a portal leading deep underground."	
		items 	= ("mural" = mural2)
		mov 	= True
		--THIS SHOULD PROBABLY BE FIXED
		--mov 	= False

gameLocations 	= ("Camp"=camp,"Mound"=mound,"Mound2"=mound2,"Mound3"=mound3,"Mountains"=mountains,"Ruins"=ruins,"UnderRuins"=underRuins,"Murals"=murals,"UnderMurals"=underMurals,"Nowhere"=Location)
initGame 		= Game () () gameLocations

---------------------------------------------------------------------------------------------------------------------
-- IO Functions
---------------------------------------------------------------------------------------------------------------------

--
-- Start the game
--
startGame message playerName = send
	where
		player 	= Player playerName 100 "Camp"
		game 	= updatePlayer player initGame
		actor 	= spawn "madnessLoop" madnessLoop [game]
		send 	=
			case message of
				Intro 	-> [message,playerName,clientName] :>> actor
				Start 	-> [message,playerName,clientName] :>> actor

--
-- Controls
--
at 						= Nothing
walk cardinal pName 	= [Walk,cardinal,pName,clientName] 		:>> "madnessLoop"
look at itemName pName  = [Look,itemName,pName,clientName] 		:>> "madnessLoop"
view pName 				= [View,pName,clientName] 				:>> "madnessLoop"
pickup itemName pName 	= [Pickup,itemName,pName,clientName] 	:>> "madnessLoop"
putdown itemName pName 	= [Putdown,itemName,pName,clientName] 	:>> "madnessLoop"
inventory pName 		= [Inventory,pName,clientName] 			:>> "madnessLoop"
talk text pName 		= [Talk,text,pName,clientName] 			:>> "madnessLoop"
status pName 			= [Status,pName,clientName]				:>> "madnessLoop"


--
-- Narration
--
narrateGame game = do
	postNarration (game::narration) 
	game{narration = ""}

loop playerName myName game
	| playerName == myName 	= madnessLoop $ narrateGame game
	| otherwise				= madnessLoop game{narration=""}

--
-- Main Game loop
--
madnessLoop game = 
	receive
		[Intro,pName,myName]			-> loop pName myName $ addPlayerLocationNarration pName game
		[Start,pName,myName] 			-> loop pName myName $ addPlayerLocationNarration pName game
		[View,pName,myName] 			-> loop pName myName $ addPlayerLocationNarration pName game
		[Walk,cardinal,pName,myName]	-> loop pName myName $ movePlayer cardinal (getPlayer pName game) game
		[Look,itemName,pName,myName]	-> loop pName myName $ lookAtItem itemName (getPlayer pName game) game
		[Pickup,itemName,pName,myName]	-> loop pName myName $ pickupItemFromLocation itemName (getSomething pName game) game
		[Putdown,itemName,pName,myName]	-> loop pName myName $ putdownItem itemName (getSomething pName game) game
		[Inventory,pName,myName]		-> loop pName myName $ narrateInventory (getSomething pName game) game
		[Status,pName,myName]			-> loop pName myName $ narrateStatus (getSomething pName game) game
		[Talk,text,pName,myName]		-> madnessLoop . narrateGame $ narrateStatus (getSomething pName game) game
		Finish							-> game
//function to flatten a nestled array
{function flatten(array){
    var flat = [];
    for (var i = 0, l = array.length; i < l; i++){
        var type = Object.prototype.toString.call(array[i]).split(' ').pop().split(']').shift().toLowerCase();
        if (type) { flat = flat.concat(/^(array|collection|arguments|object)$/.test(type) ? flatten(array[i]) : array[i]); }
    }
    return flat;
}}start = (whitespace / lexeme )+

lexeme = (varid_e / conid_e / varsym_e / consym_e / qvarid / qconid / qvarsym / qconsym / literal / special / reservedop / reservedid)
literal = ( integer / float / char  / string )
special = s:( "(" / ")" / "," / ";" / "[" / "]" / "`" / "{" / "}" ) {return {val: s, typ: s}}

whitespace = w:( whitestuff )+ {return {val: w.join("")}}
whitestuff = ( whitechar / comment / ncomment )
whitechar = ( newline / vertab / space / tab ) 
newline =  ( return linefeed / linefeed / formfeed )
return = "\r"
linefeed = "\n"
vertab = "\v"
formfeed = "\f"
space = " "
tab = "\t"

comment = ds:dashes ((!symbol any) any*)? n:newline {return n}
dashes = d:"-"ds:("-")+ {return d.concat(ds.join(""))}
opencom = "{-"
closecom = "-}" 
ncomment = !symbol com:(opencom ANYseq (ncomment ANYseq)* closecom) {return flatten(com).join("").replace(/\S/g," ");}
ANYseq = as:ANY* {return as.join("")} 
ANY = !(opencom / closecom) a:( graphic / whitechar ) {return a}
any = ( graphic / space / tab ) 
graphic = g:( small / large / symbol / digit / special / "\"" / "'" ) {return (g instanceof Object? g.val:g)}

small = ( ascSmall / "_" )
ascSmall = [a-z]

large = ascLarge
ascLarge = [A-Z]

symbol = s:ascSymbol {return s}
ascSymbol = !(opencom / closecom) s:( "!" / "#" / "$" / "%" / "&" / "*" / "+" / "." / "/" / "<" / ">" / "=" / "?" / "@" / "\\" / "^" / "|" / "-" / "~" / ":" ) {return s}

digit = ascDigit
ascDigit = [0-9]
octit = [0-7]
hexit = ( digit / [A-F] / [a-f] )
 
varid = ( !reservedid head:small tail:( small / large / digit / "'" )* ) {return head.concat(tail.join(""))}
conid = ( head:large tail:( small / large / digit / "'" )* ) {return head.concat(tail.join(""))}
reservedid = res:( "hiding" / "case" / "class" / "data" / "default" / "deriving" / "do" / "else" / "foreign" / "if" / "import" / "infixl" / "infixr" / "infix" / "instance" / "in" / "let" / "def" / "module" / "newtype" / "of" / "then" / "type" / "where" / "_" ) !(varid / reservedid) {return {val: res, typ: res}}

varsym = !(reservedop / dashes / escape / ":") s:(symbol)+ {return s.join("")}
consym = !reservedop s:(":"symbol*) {return flatten(s).join("")}
reservedop = op:( ".." / "::" / ":" / "=>" / "=" !"="  / "\\" !escapestuff / "|" !"|" / "<-" / "->" / "@" / "~" ) !(varsym / consym) {return {val: (op instanceof Array)? op.join(""): op, typ: (op instanceof Array)? op.join(""): op}}

qvarid = qs:(conid ".")+ ref:((!"." varid) !".") {qs = flatten(qs).join(""); return {val: flatten([qs, ref[0]]).join(""), typ: "qvarid", qual: qs.substr(0,qs.length-1)}}
qconid = qs:(conid ".")+ ref:((!"." conid) !".") {qs = flatten(qs).join(""); return {val: flatten([qs, ref[0]]).join(""), typ: "qconid", qual: qs.substr(0,qs.length-1)}}
qvarsym = qs:(conid ".")+ ref:(varsym !".") {qs = flatten(qs).join(""); return {val: flatten([qs, ref[0]]).join(""), typ: "qvarsym", qual: qs.substr(0,qs.length-1)}}
qconsym = qs:(conid ".")+ ref:((!"." consym) !".") {qs = flatten(qs).join(""); return {val: flatten([qs, ref[0]]).join(""), typ: "qconsym", qual: qs.substr(0,qs.length-1)}}

varid_e = !"." v:varid !"." {return {val: v, typ: "varid"}}
conid_e = !"." c:conid !"." {return {val: c, typ: "conid"}}
varsym_e = v:varsym !"."    {return {val: v, typ: "varsym"}}
consym_e = !"." c:consym !"." {return {val: c, typ: "consym"}}

decimal = d:digit+ {return d.join("")}
octal = o:octit+ {return o.join("")}
hexadecimal = h:hexit+ {return h.join("")}

integer = i:( notdecimal / decimal !("o" /"O" / "x" / "X")) !"." {return {val: i.join(""), typ: "integer"}}
notdecimal = ( "0o" octal / "0O" octal / "0x" hexadecimal / "0X" hexadecimal )

float = f:( (decimal "." decimal (exponent)?) / decimal exponent ) {return {val: f.join(""), typ: "float"}}
exponent = e:(("e"/"E") ("+"/"-")? decimal) {return e.join("")}

char = ("'"c:( !("'" / "\\") graphic / space / !"\\&" escape)"'" ) {return {val: ("'" + c.join("") + "'"), typ: "char"}}
string = "\"" bod:stringchar* "\"" {return {val: ('"' + bod.join("") + '"'), typ: "string"}}
stringchar =  !("\"") ch:(space / escape / graphic / gap)  {return ch}
escape = esc:("\\" escapestuff) {return esc.join("")} 
escapestuff = ( charesc / ascii / decimal / "o" octal / "x" hexadecimal)
charesc = ( "a" / "b" / "f" / "n" / "r" / "t" / "v" / "\\" / "\"" / "'" / "&" )
ascii = ( "^" cntrl "&#0" / "&#1" / "&#2" / "&#3" / "&#4" / "&#5" / "&#6" / "&#7" / "&#8" / "&#9" / "&#10" / "&#11" / "&#12" / "&#13" / "&#14" / "&#15" / "&#16" / "&#17" / "&#18" / "&#19" / "&#20" / "&#21" / "&#22" / "&#23" / "&#24" / "&#25" / "&#26" / "&#27" / "&#28" / "&#29" / "&#30" / "&#31" / "&#32" / "&#127" )
cntrl = ( ascLarge / "@" / "[" / "\\" / "]" / "^" / "_" )
gap = "\\" whitechar+ "\\"

